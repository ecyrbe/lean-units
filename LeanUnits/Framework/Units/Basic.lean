import LeanUnits.Framework.Dimensions.Basic
import LeanUnits.Framework.Conversion
import LeanUnits.Framework.Utils
import Mathlib.Data.DFinsupp.Defs
import Mathlib.Data.DFinsupp.BigOperators
import Batteries.Data.Rat.Basic
import Mathlib.Data.Rat.Defs

namespace Units

/--
Choosing a metric involves choosing
- a power factor (it's one for base and derived units)
- an affine conversion (to convert between units of the same dimension)
- a dimension (to ensure dimensional correctness)

Note that all these 3 choices form a AddCommGroup, so we can combine them easily.
-/
abbrev UnitChoice := â„š Ã— Conversion Ã— Dimension

/--
A unit is a product of base units raised to rational powers.
For example, the unit of force in the SI system is `kgâ€¢m/sÂ²`
can be represented as :
- `forceâ‚ = {"kg" â†¦ (1,0,Mass), "m" â†¦ (1,0,Length), "s" â†¦ (-2,0,Timeâ»Â²)}`

or when using derived units:
- `forceâ‚‚={"N" â†¦ (1,0,Massâ€¢Lengthâ€¢Timeâ»Â²)}`

Converting between these two representations is possible because
their dimensions are the same under product:
- `Î  ð’Ÿ(forceâ‚) = Massâ€¢Lengthâ€¢Timeâ»Â² = Î  ð’Ÿ(forceâ‚‚)`
-/
structure Unit where
  _impl : DFinsupp (fun _ : String =>  UnitChoice)
  deriving DecidableEq, BEq

namespace Unit

instance instEquiv : Unit â‰ƒ (DFinsupp (fun _ : String => UnitChoice)) where
  toFun := Unit._impl
  invFun := Unit.mk

instance instAddCommGroup : AddCommGroup Unit :=
  Unit.instEquiv.addCommGroup

-- implement convenient syntax for units, because addition is confusing
@[simp]
instance : One Unit where
  one := 0

@[simp]
instance : Mul Unit where
  mul u1 u2 := u1 + u2

@[simp]
instance : Inv Unit where
  inv u := -u

@[simp]
instance : Div Unit where
  div u1 u2 := u1 - u2

@[simp]
instance : Pow Unit â„• where
  pow u q := q â€¢ u

@[simp]
instance : Pow Unit â„¤ where
  pow u n := n â€¢ u

def dimension (u : Unit) : Dimension :=
  u._impl.sum (fun _ qd => qd.2.2)

def conversion (u : Unit) : Conversion :=
  u._impl.sum (fun _ qd => qd.2.1)

instance : HasDimension Unit where
  dimension := Unit.dimension

instance : HasConversion Unit where
  conversion := Unit.conversion

/--
Two units are considered equal if they have the same dimension and conversion,
regardless of their representation.

For example, `N` and `kgâ€¢m/sÂ²` are considered equivalent
-/
@[simp]
instance instSetoidUnit : Setoid Unit where
  r:= fun uâ‚ uâ‚‚ => uâ‚.dimension = uâ‚‚.dimension âˆ§ uâ‚.conversion = uâ‚‚.conversion
  iseqv := âŸ¨
    fun _u => âŸ¨rfl, rflâŸ©,
    fun {_uâ‚ _uâ‚‚} âŸ¨h1, h2âŸ© => âŸ¨h1.symm, h2.symmâŸ©,
    fun {_uâ‚ _uâ‚‚ _uâ‚ƒ} âŸ¨h1, h2âŸ© âŸ¨h3, h4âŸ© =>
      âŸ¨h1.trans h3, h2.trans h4âŸ©
  âŸ©

-- decidable equivalence of units
instance instDecidableEquivUnit (u1 u2 : Unit) : Decidable (u1 â‰ˆ u2) := by
  dsimp [HasEquiv.Equiv, instSetoidUnit]
  infer_instance

/--
Define a base unit with a given name and dimension.

Example:
- `meter = defineUnit "m" Dimension.Length`
-/
def defineUnit (s : String) (d : Dimension) : Unit := âŸ¨DFinsupp.single s (1,0,d)âŸ©

/--
Define a derived unit with a given name, unit and optional conversion.
Example:
- `newton = defineDerivedUnit "N" (kilogram * meter / second^2)`
- `celsius = defineDerivedUnit "Â°C" kelvin (Conversion.translate (27315/100))`
-/
def defineDerivedUnit (s : String) (u : Unit)
  (conv : Conversion := 0) : Unit := âŸ¨DFinsupp.single s (1,conv.div u.conversion,u.dimension)âŸ©

section Repr
open Lean Parser Term

unsafe instance : Repr Unit where
  reprPrec f _ :=
    let vals := f._impl.support'.unquot.val.map (fun i => (i,(f._impl i)))
      |>.unquot
      |>.filter (Â·.2 != 0)
      |>.dedup
      |>.mergeSort (fun a b => a.1 < b.1)
    if vals.length = 0 then
      "âˆ…"
    else
      let parts : List String := vals.map (fun a => formatExp a.1 a.2.1.1)
      f!"{String.intercalate "â€¢" parts}"

end Repr

end Unit

end Units
